import QtQuick
import QtQuick.Layouts
import Quickshell
import qs.Commons
import qs.Ui

Panel {
  id: root
  moduleName: "ryu.thermals"
  manageIpc: false

  property var anchorItem: null
  property var hostWidget: null

  readonly property var w: hostWidget

  function open() {
    root.controller.show()
  }

  function close() {
    root.controller.hide()
  }

  function switchPanel(direction) {
    if (root.bar && typeof root.bar.switchPanelFrom === "function")
      return root.bar.switchPanelFrom(root.hostWidget || root, direction)
    return false
  }

  KeyboardPanel {
    id: panel
    anchorItem: root.anchorItem
    owner: root.hostWidget || root
    bar: root.bar
    open: root.opened
    focusTarget: keyCatcher
    contentWidth: panel.fittedContentWidth(Style.space(260))
    contentHeight: panel.fittedContentHeight(content.implicitHeight)

    PanelKeyCatcher {
      id: keyCatcher
      anchors.fill: parent
      onCloseRequested: root.close()
      onTabRequested: function(direction) { root.switchPanel(direction) }

      Column {
        id: content
        width: parent.width
        spacing: Style.space(8)

        Text {
          width: parent.width
          text: "Thermals"
          color: root.barForeground
          font.family: root.bar ? root.bar.fontFamily : Style.font.family
          font.pixelSize: Style.font.subtitle
          font.bold: true
        }

        Repeater {
          model: [
            { label: "CPU Temp", value: root.w ? root.w.cpuTemp + " °C" : "-" },
            { label: "CPU Fan", value: root.w && root.w.cpuFan !== "-" ? root.w.cpuFan + " RPM" : "n/a" },
            { label: "GPU Temp", value: root.w ? root.w.gpuTemp + " °C" : "-" },
            { label: "GPU Fan", value: root.w && root.w.gpuFan !== "-" ? root.w.gpuFan + " RPM" : "n/a" }
          ]

          delegate: RowLayout {
            required property var modelData
            width: parent ? parent.width : 0
            spacing: Style.space(8)

            Text {
              Layout.fillWidth: true
              text: parent.modelData.label
              color: root.barForeground
              opacity: 0.7
              font.family: root.bar ? root.bar.fontFamily : Style.font.family
              font.pixelSize: Style.font.body
            }

            Text {
              text: parent.modelData.value
              color: root.barForeground
              font.family: root.bar ? root.bar.fontFamily : Style.font.family
              font.pixelSize: Style.font.body
              font.bold: true
            }
          }
        }
      }
    }
  }
}
